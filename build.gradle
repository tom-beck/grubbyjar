plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'net.ltgt.errorprone' version '0.0.12'
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile("com.github.jengelman.gradle.plugins:shadow:2.0.1")
    implementation "com.google.guava:guava:23.0"
    implementation "org.jruby:jruby-complete:9.1.12.0"
    implementation "commons-io:commons-io:2.5"

    testCompile "junit:junit:4.12"
    testCompile "org.apache.commons:commons-lang3:3.6"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.mockito:mockito-core:2.10.0"
    testCompile "org.zeroturnaround:zt-exec:1.10"
}

group = 'ca.neitsch'
version = '0.0.3'

gradlePlugin {
    plugins {
        grubbyjarPlugin {
            id = 'ca.neitsch.grubbyjar'
            implementationClass = 'ca.neitsch.grubbyjar.GrubbyjarPlugin'
        }
    }
}

pluginBundle {
  website = 'https://github.com/andrewdotn/grubbyjar'
  vcsUrl = 'https://github.com/andrewdotn/grubbyjar'
  description = 'Builds self-contained jars for jruby apps'
  tags = ['jruby', 'fatjar', 'shadow']

  plugins {
    grubbyjarPlugin {
      id = 'ca.neitsch.grubbyjar'
      displayName = 'grubbyjar'
    }
  }
}

tasks.withType(AbstractCompile) {
    options.compilerArgs << "-Werror" << "-Xlint" << "-Xlint:-serial"
}

task unitTest(type: Test) {
    exclude '**/*IntegTest.class'
}

task integTest(type: Test) {
    include '**/*IntegTest.class'
    shouldRunAfter unitTest
}

// Declared as type Test with no tests, because that makes IntelliJ listen for
// test results.
task unitThenIntegrationTests(type: Test) {
    exclude '*'
    dependsOn unitTest, integTest
}

// Run outside of TestKit to be sure weâ€™re handling dependencies as expected
task standaloneTestPrep(type: Exec) {
    commandLine './gradlew', 'clean', 'grubbyjar'
    workingDir 'standaloneTest'

    dependsOn publishToMavenLocal
    shouldRunAfter integTest, test
}
task standaloneTest(type: Exec) {
    commandLine 'java', '-jar', 'build/libs/standaloneTest-grubbyjar.jar'
    workingDir 'standaloneTest'

    dependsOn standaloneTestPrep
}

task testall(type: Test) {
    exclude '*'
    dependsOn unitThenIntegrationTests
    dependsOn standaloneTest
}
